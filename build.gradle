/*
 *     Copyright (C) 2018  Hyperium <https://hyperium.cc/>
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU Lesser General Public License as published
 *     by the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU Lesser General Public License for more details.
 *
 *     You should have received a copy of the GNU Lesser General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

version = "4.1.2"

buildscript {
    repositories {
        maven {
            url "https://hyperiumjailbreak.mycloudrepo.io/public/repositories/gradle-plugin-proxy"
        }
        maven {
            url "https://hyperiumjailbreak.mycloudrepo.io/public/repositories/libs"
        }
        maven {
            url "https://hyperiumjailbreak.mycloudrepo.io/public/repositories/forge-proxy"
        }
        maven {
            url "https://hyperiumjailbreak.mycloudrepo.io/public/repositories/sponge-proxy"
        }
    }
    dependencies {
        classpath "com.hyperiumjailbreak.gradle:ForgeGradle:2.1-SNAPSHOT"
        classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

apply plugin: "com.hyperiumjailbreak.minecraft-tweaker"
apply plugin: "org.spongepowered.mixin"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "java"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

archivesBaseName = "Hyperium"
compileJava.options.encoding = "UTF-8"
compileJava.options.compilerArgs << "-Xlint:none"

minecraft {
    version "1.8.9"
    tweakClass "cc.hyperium.launch.HyperiumTweaker"
    mappings "stable_20"
    runDir "run"
}

dependencies {
    embed("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        exclude module: "launchwrapper"
        exclude module: "guava"
        exclude module: "gson"
        exclude module: "commons-io"
    }
    embed "com.github.ben-manes.caffeine:caffeine:2.8.0"
    embed group: "com.google.guava", name: "guava", version: "17.0"
    embed group: "com.google.code.gson", name: "gson", version: "2.8.5"
    embed("org.slick2d:slick2d-core:1.0.2") {
        exclude module: "lwjgl"
    }
    embed "org.jetbrains:annotations:20.0.0"
    embed "org.apache.httpcomponents:httpclient:4.5.11"
    embed "org.apache.httpcomponents:httpcore:4.4.5"
}

mixin {
    defaultObfuscationEnv notch
    add sourceSets.main, "mixin.hyperium.refmap.json"
    disableRefMapWarning true
}

repositories {
    maven {
        url "https://hyperiumjailbreak.mycloudrepo.io/public/repositories/libs"
    }
    maven {
        url "https://hyperiumjailbreak.mycloudrepo.io/public/repositories/sponge-proxy"
    }
}

processResources {
    inputs.files "src/main/resources"
    outputs.dir "build/classes/main"
    copy {
        from("src/main/resources")
        into("build/classes/main")
    }
}

jar {
    dependsOn configurations.compile
    from {
        configurations.embed.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
    manifest.attributes(
        "MixinConfigs": "mixins.hyperium.json",
        "TweakClass": "cc.hyperium.launch.HyperiumTweaker",
        "TweakOrder": 0,
        "Manifest-Version": 1.0,
        "Main-Class": "com.hyperiumjailbreak.Runtime"
    )
    classifier "base"
}

shadowJar {
    from jar
    baseName "Hyperium"
    classifier null
    version rootProject.version
    relocate "org.apache.http", "cc.hyperium.installer.utils.http"
}

build {
    dependsOn shadowJar
}
