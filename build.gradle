/*
 *     Copyright (C) 2018  Hyperium <https://hyperium.cc/>
 *
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU Lesser General Public License as published
 *     by the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU Lesser General Public License for more details.
 *
 *     You should have received a copy of the GNU Lesser General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

version = "4.2.0"

buildscript {
    repositories {
        maven {
            url "https://hyperiumjailbreak.mycloudrepo.io/public/repositories/gradle-plugin-proxy"
        }
        maven {
            url "https://hyperiumjailbreak.mycloudrepo.io/public/repositories/libs"
        }
        maven {
            url "https://hyperiumjailbreak.mycloudrepo.io/public/repositories/forge-proxy"
        }
    }
    dependencies {
        classpath "com.hyperiumjailbreak.gradle:ForgeGradle:2.1.1-SNAPSHOT"
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

apply plugin: "com.hyperiumjailbreak.minecraft-tweaker"
apply plugin: "com.hyperiumjailbreak.mixingradle"
apply plugin: "java"
apply plugin: "maven"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

archivesBaseName = "Hyperium"
compileJava.options.encoding = "UTF-8"
compileJava.options.compilerArgs << "-Xlint:none"

minecraft {
    version "1.8.9"
    tweakClass "cc.hyperium.launch.HyperiumTweaker"
    mappings "stable_20"
}

dependencies {
    annotationProcessor "org.spongepowered:mixin:0.7.11-SNAPSHOT"
    embed ("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        exclude module: "launchwrapper"
        exclude module: "guava"
        exclude module: "gson"
        exclude module: "commons-io"
    }
    embed "com.github.ben-manes.caffeine:caffeine:2.8.0"
    embed group: "com.google.guava", name: "guava", version: "17.0"
    embed group: "com.google.code.gson", name: "gson", version: "2.8.5"
    embed ("org.slick2d:slick2d-core:1.0.2") {
        exclude module: "lwjgl"
    }
    embed "com.github.HyperiumClient:Installer:1.9"
}

mixin {
    defaultObfuscationEnv notch
    add sourceSets.main, "mixin.hyperium.refmap.json"
}

repositories {
    maven {
        url "https://hyperiumjailbreak.mycloudrepo.io/public/repositories/libs"
    }
    maven {
        url "https://hyperiumjailbreak.mycloudrepo.io/public/repositories/sponge-proxy"
    }
}

processResources {
    inputs.files "src/main/resources"
    outputs.dir "build/classes/main"
    copy {
        from("src/main/resources")
        into("build/classes/main")
    }
}

javadoc {
    options {
        encoding "UTF-8"
    }
    exclude "cc/hyperium/mixins/**"
    // code developed externally
    exclude "me/semx11/**"
    exclude "net/minecraftforge/**"
    exclude "net/hypixel/**"
}

jar {
    dependsOn configurations.compile
    from {
        configurations.embed.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
    manifest.attributes(
        "Manifest-Version": 1.0,
        "TweakOrder": 0,
        "MixinConfigs": "mixins.hyperium.json",
        "Implementation-Title": "HyperiumJailbreak",
        "Implementation-Version": project.version,
        "TweakClass": "cc.hyperium.launch.HyperiumTweaker",
        "Main-Class": "com.hyperiumjailbreak.Runtime"
    )
    configurations.embed.each { dep ->
        from (project.zipTree(dep)) {
            exclude "org/jetbrains/annotations", "org/jetbrains/annotations/**"
        }
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    description "Builds a jar containing the source code."
    classifier "sources"
    group "build"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc
    description "Builds a jar containing the javadocs."
    classifier "javadoc"
    group "build"
}

artifacts {
    archives jar
    archives javadocJar
    archives sourceJar
}
